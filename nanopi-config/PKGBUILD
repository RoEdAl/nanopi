pkgname=nanopi-config
pkgver=1
pkgrel=1
pkgdesc='NanoPi-specific configuration files'
arch=('arm')
url='http://nanopi.org'
license=('GPL2')
install=${pkgname}.install
depends=('systemd>=215' 'netctl' 'wpa_supplicant' 'bluez' 'bluez-utils' 'openssh' 'dhcp' 'haveged')
optdepends=('wpa_actiond' 'dialog')
backup=('etc/fstab' 'etc/dhcpd.conf')
source=('gadet-friendlyarm.network'
	'ap6210.tar.gz' 
        'fstab'
        'dhcpd.conf'
        'journald.conf'
        'haveged-sched.conf'
        'systemd-timesyncd-sched.conf'
        'sshd-sched.conf')
md5sums=('4ea74fb21b0642242a5d72fad6301f92'
         'fc95963e66974e82b1124abf3f152eb2'
         'a27a95989581eb85671ca5cdd57c4bcd'
         'd709cb2d401baaadd78d7f4c1e8f6f79'
         'f3f0fd8c65905373b97060f6f232041d'
         '04c50377dafb9620aca9abffabe54f6a'
         '04c50377dafb9620aca9abffabe54f6a'
         'aa8f2773df9103538ccf6684929c05c6')

package() {
  cd ${srcdir}

  msg2 'ap6210 firmware'
  mkdir -p "${pkgdir}/usr/lib/firmware"
  cp -r --preserve=mode ap6210 "${pkgdir}/usr/lib/firmware"
  
  msg2 'systemd-networkd configuration'
  mkdir -p "${pkgdir}/usr/lib/systemd/network"
  install -m644 gadet-friendlyarm.network  "${pkgdir}/usr/lib/systemd/network/70-gadet-frienlyarm.network"

  msg2 'DHCP configuration'
  mkdir -p "${pkgdir}/etc"
  install -m644 dhcpd.conf "${pkgdir}/etc"

  msg2 'journalling configuration'
  mkdir -p "${pkgdir}/usr/lib/systemd/journald.conf.d"
  install -m644 journald.conf "${pkgdir}/usr/lib/systemd/journald.conf.d/10-nanopi.conf"

  msg2 'fstab'
  install -m644 fstab "${pkgdir}/etc"

  msg2 'haveged'
  mkdir -p "${pkgdir}/usr/lib/systemd/system/haveged.service.d"
  install -m644 haveged-sched.conf "${pkgdir}/usr/lib/systemd/system/haveged.service.d/sched.conf"

  msg2 'systemd-timesyncd'
  mkdir -p "${pkgdir}/usr/lib/systemd/system/systemd-timesyncd.service.d"
  install -m644 systemd-timesyncd-sched.conf "${pkgdir}/usr/lib/systemd/system/systemd-timesyncd.service.d/sched.conf"

  msg2 "sshd@"
  mkdir -p "${pkgdir}/usr/lib/systemd/system/sshd@.service.d"
  install -m644 sshd-sched.conf "${pkgdir}/usr/lib/systemd/system/sshd@.service.d/sched.conf"
}
